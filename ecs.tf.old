resource "aws_ecs_cluster" "kvs_dg_integrator_cluster" {
  name = "kvs-dg-integrator-cluster"
  setting {
    name  = "containerInsights"
    value = "disabled"
  }
}

resource "aws_ecs_task_definition" "kvs_dg_integrator" {
  family                   = "kvs-dg-integrator-task-family"
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  cpu                      = var.kvs_dg_integrator_task_cpu
  memory                   = var.kvs_dg_integrator_task_memory
  execution_role_arn       = aws_iam_role.kvs_dg_integrator_execution_role.arn
  task_role_arn            = aws_iam_role.kvs_dg_integrator_task_role.arn

  container_definitions = jsonencode([
    {
      name      = "kvs-dg-integrator-container"
      image     = var.kvs_dg_integrator_image
      portMappings = [{
        containerPort = 80
        hostPort      = 80
        protocol      = "tcp"
      }]
      environment = [
        { name = "DEEPGRAM_API", value = var.deepgram_api },
        { name = "DEEPGRAM_API_KEY", value = var.deepgram_api_key },
        { name = "LOG_LEVEL", value = var.kvs_dg_integrator_log_level },
        { name = "APP_REGION", value = data.aws_region.current.name }
      ]
      logConfiguration = {
        logDriver = "awslogs"
        options = {
          awslogs-group         = "/ecs/kvs-dg-integrator"
          awslogs-region        = "${var.environment.region}"
          awslogs-stream-prefix = "ecs"
        }
      }
    }
  ])
}